!*********************************************************************
!* Illinois Open Source License                                      *
!*                                                                   *
!* University of Illinois/NCSA                                       * 
!* Open Source License                                               *
!*                                                                   *
!* Copyright@2008, University of Illinois.  All rights reserved.     *
!*                                                                   *
!*  Developed by:                                                    *
!*                                                                   *
!*     Center for Simulation of Advanced Rockets                     *
!*                                                                   *
!*     University of Illinois                                        *
!*                                                                   *
!*     www.csar.uiuc.edu                                             *
!*                                                                   *
!* Permission is hereby granted, free of charge, to any person       *
!* obtaining a copy of this software and associated documentation    *
!* files (the "Software"), to deal with the Software without         *
!* restriction, including without limitation the rights to use,      *
!* copy, modify, merge, publish, distribute, sublicense, and/or      *
!* sell copies of the Software, and to permit persons to whom the    *
!* Software is furnished to do so, subject to the following          *
!* conditions:                                                       *
!*                                                                   *
!*                                                                   *
!* @ Redistributions of source code must retain the above copyright  * 
!*   notice, this list of conditions and the following disclaimers.  *
!*                                                                   * 
!* @ Redistributions in binary form must reproduce the above         *
!*   copyright notice, this list of conditions and the following     *
!*   disclaimers in the documentation and/or other materials         *
!*   provided with the distribution.                                 *
!*                                                                   *
!* @ Neither the names of the Center for Simulation of Advanced      *
!*   Rockets, the University of Illinois, nor the names of its       *
!*   contributors may be used to endorse or promote products derived * 
!*   from this Software without specific prior written permission.   *
!*                                                                   *
!* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   *
!* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   *
!* OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          *
!* NONINFRINGEMENT.  IN NO EVENT SHALL THE CONTRIBUTORS OR           *
!* COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER       * 
!* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,   *
!* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE    *
!* USE OR OTHER DEALINGS WITH THE SOFTWARE.                          *
!*********************************************************************
!* Please acknowledge The University of Illinois Center for          *
!* Simulation of Advanced Rockets in works and publications          *
!* resulting from this software or its derivatives.                  *
!*********************************************************************
! ******************************************************************************
!
! Purpose: Collection of routines for JWL equation of state. 
!
! Description: None.
!
! Notes: None. 
!
! ******************************************************************************
!
! $Id: SPEC_RFLU_ModPBA.F90_SAVE_20110315,v 1.1.1.1 2015/01/23 22:57:50 tbanerjee Exp $
!
! Copyright: (c) 2006-2007 by the University of Illinois
!
! ******************************************************************************

MODULE SPEC_RFLU_ModPBA

  USE ModParameters
  USE ModDataTypes  
  USE ModGlobal, ONLY: t_global
  USE ModGrid, ONLY: t_grid
  USE ModDataStruct, ONLY: t_region
  USE ModMixture, ONLY: t_mixt_input
  USE ModError
  USE ModMPI

#ifdef SPEC
  USE ModSpecies, ONLY: t_spec_input, &
                        t_spec_type
  USE ModInterfacesSpecies, ONLY: SPEC_GetSpeciesIndex
#endif

  USE RFLU_ModConvertCv, ONLY: RFLU_ScalarConvertCvCons2Prim, &
                               RFLU_ScalarConvertCvPrim2Cons

  USE ModInterfaces, ONLY: MixtPerf_C_GRT, &
                           MixtPerf_G_CpR, &
                           MixtPerf_P_DEoGVm2, &
                           MixtPerf_R_M, &
                           MixtPerf_T_DPR

  IMPLICIT NONE

! ******************************************************************************
! Declarations and definitions
! ******************************************************************************

! ==============================================================================
! Private data
! ==============================================================================

  CHARACTER(CHRLEN), PRIVATE :: & 
    RCSIdentString = '$RCSfile: SPEC_RFLU_ModPBA.F90_SAVE_20110315,v $'
  
! ==============================================================================
! Public functions
! ==============================================================================

  PUBLIC :: SPEC_RFLU_PBA_Eo_GUd, &
            SPEC_RFLU_PBA_GetCJStates, &
            SPEC_RFLU_PBA_ProgramBurn, &
            SPEC_RFLU_PBA_ZeroResiduals

! ==============================================================================
! Private functions
! ==============================================================================
  
  PRIVATE ::

! ******************************************************************************
! Routines
! ******************************************************************************

  CONTAINS
  
  




! ******************************************************************************
!
! Purpose: Compute energy of explosive for program burn.
!
! Description: None.
!
! Input:
!   g             Ratio of specific heats
!   ud            Detonation velocity
!
! Output: None.
!
! Notes: None.
!
! ******************************************************************************

  FUNCTION SPEC_RFLU_PBA_Eo_GUd(g,ud)

    IMPLICIT NONE
        
! ******************************************************************************
!   Declarations and definitions
! ******************************************************************************
    
! ==============================================================================
!   Arguments
! ==============================================================================
    
    REAL(RFREAL), INTENT(IN) :: g,ud
    REAL(RFREAL) :: SPEC_RFLU_PBA_Eo_GUd
    
! ******************************************************************************
!   Start
! ******************************************************************************
   
    SPEC_RFLU_PBA_Eo_GUd = ((ud)**2.0_RFREAL)/(2.0_RFREAL*(g*g-1.0_RFREAL))

! ******************************************************************************
!   End  
! ******************************************************************************

  END FUNCTION SPEC_RFLU_PBA_Eo_GUd








! ******************************************************************************
!
! Purpose: Compute CJ state for program burn.
!
! Description: None.
!
! Input:
!   g             Ratio of specific heats
!   ro            Density of explosive
!   ud            Detonation velocity
!
! Output:
!   e             Internal energy of products
!   r             Density of products
!   u             Velocity of products
!
! Notes: None.
!
! ******************************************************************************

  SUBROUTINE SPEC_RFLU_PBA_GetCJStates(g,ro,ud,e,r,u)

    IMPLICIT NONE
        
! ******************************************************************************
!   Declarations and definitions
! ******************************************************************************
    
! ==============================================================================
!   Arguments
! ==============================================================================
    
    REAL(RFREAL), INTENT(IN) :: g,ro,ud
    REAL(RFREAL), INTENT(OUT) :: e,r,u
    
! ******************************************************************************
!   Start
! ******************************************************************************
  
    e = g*ud*ud/((g+1.0_RFREAL)*(g*g-1.0_RFREAL))
    r = ro*(g+1.0_RFREAL)/g
    u = ud/(g+1.0_RFREAL)

! ******************************************************************************
!   End  
! ******************************************************************************

  END SUBROUTINE SPEC_RFLU_PBA_GetCJStates








! ******************************************************************************
!
! Purpose: Program burn algorithm.
!
! Description: None.
!
! Input: None.
!
! Output: None.
!
! Notes: None.
!
! ******************************************************************************

  SUBROUTINE SPEC_RFLU_PBA_ProgramBurn(pRegion)

    IMPLICIT NONE
        
! ******************************************************************************
!   Declarations and definitions
! ******************************************************************************
    
! ==============================================================================
!   Arguments
! ==============================================================================
   
    TYPE(t_region), POINTER :: pRegion
    
! ==============================================================================
!   Locals
! ==============================================================================

    CHARACTER(CHRLEN) :: errorString
    INTEGER :: icg,iCvSpecAir,iCvSpecExplosive,iCvSpecProducts,indCp,indMol
    REAL(RFREAL) :: a,cpProducts,detonVel,e,Eo,flameLoc,gcProducts,gProducts, &
                    mwProducts,nTol,p,r,ro,T,u,Vm2,x,xFinal,xInit,y
    REAL(RFREAL), DIMENSION(:,:), POINTER :: pCvMixt,pCvSpec,pDvMixt,pGvMixt
    TYPE(t_global), POINTER :: global
    TYPE(t_grid), POINTER :: pGrid
    TYPE(t_mixt_input), POINTER :: pMixtInput
    TYPE(t_spec_input), POINTER :: pSpecInput

! ******************************************************************************
!   Start, if pbaBurnFlag is FALSE then no need to go further
! ******************************************************************************

    global => pRegion%global

    IF ( global%pbaBurnFlag .EQV. .FALSE. ) THEN
      RETURN
    END IF ! flameLoc

    CALL RegisterFunction(global,'SPEC_RFLU_PBA_ProgramBurn',__FILE__)

! ******************************************************************************
!   Set pointers and variables 
! ******************************************************************************

    pGrid      => pRegion%grid
    pCvMixt    => pRegion%mixt%cv
    pCvSpec    => pRegion%spec%cv
    pDvMixt    => pRegion%mixt%dv
    pGvMixt    => pRegion%mixt%gv
    pMixtInput => pRegion%mixtInput
    pSpecInput => pRegion%specInput

    indCp  = pRegion%mixtInput%indCp
    indMol = pRegion%mixtInput%indMol

    nTol = 1.0E-14_RFREAL

! ******************************************************************************
!   Need species in primitive form
! ******************************************************************************

    CALL RFLU_ScalarConvertCvCons2Prim(pRegion,pRegion%spec%cv, &
                                       pRegion%spec%cvState)

! ******************************************************************************
!   Use program burn algorithm based on case name
! ******************************************************************************

    SELECT CASE ( global%casename )

! ==============================================================================
!     Jenkins 2D planar case
! ==============================================================================

      CASE ( "jenkins2d" )
        iCvSpecAir       = SPEC_GetSpeciesIndex(global,pSpecInput,'AIR')
        iCvSpecExplosive = SPEC_GetSpeciesIndex(global,pSpecInput,'EXPLOSIVE')
        iCvSpecProducts  = SPEC_GetSpeciesIndex(global,pSpecInput,'PRODUCTS')

        mwProducts = pSpecInput%specType(iCvSpecProducts)%pMaterial%molw
        cpProducts = pSpecInput%specType(iCvSpecProducts)%pMaterial%spht

        gcProducts = MixtPerf_R_M(mwProducts)
        gProducts  = MixtPerf_G_CpR(cpProducts,gcProducts)

        ro       = pMixtInput%prepRealVal6

        detonVel = pSpecInput%specType(iCvSpecExplosive)%pMaterial%detonVel
        xInit    = pMixtInput%prepRealVal1
        xFinal   = pMixtInput%prepRealVal2
        flameLoc = xInit + detonVel*(global%currentTime)

        DO icg = 1,pGrid%nCellsTot
          x = pGrid%cofg(XCOORD,icg)
          y = pGrid%cofg(YCOORD,icg)

          IF ( x >= pMixtInput%prepRealVal1 .AND. &
               x <= pMixtInput%prepRealVal2 .AND. &
               y >= pMixtInput%prepRealVal3 .AND. &
               y <= pMixtInput%prepRealVal4 ) THEN             
            IF ( flameLoc > pGrid%cofg(XCOORD,icg) .AND. &
                 ABS(1.0_RFREAL - pCvSpec(iCvSpecExplosive,icg)) < nTol ) THEN
! ----------- Set mass fractions for products and explosive --------------------
              pCvSpec(iCvSpecExplosive,icg) = 0.0_RFREAL
              pCvSpec(iCvSpecProducts,icg)  = 1.0_RFREAL

! ----------- Set Cv for products ----------------------------------------------
              CALL SPEC_RFLU_PBA_GetCJStates(gProducts,ro,detonVel,e,r,u)

              pCvMixt(CV_MIXT_DENS,icg) = r
              pCvMixt(CV_MIXT_XMOM,icg) = r*u
              pCvMixt(CV_MIXT_YMOM,icg) = 0.0_RFREAL
              pCvMixt(CV_MIXT_ZMOM,icg) = 0.0_RFREAL
              pCvMixt(CV_MIXT_ENER,icg) = r*(e+0.5_RFREAL*u*u)

! ----------- Set Dv for products ----------------------------------------------
              Vm2 = u*u
              Eo  = e+0.5_RFREAL*u*u
              p   = MixtPerf_P_DEoGVm2(r,Eo,gProducts,Vm2)
              T   = MixtPerf_T_DPR(r,p,gcProducts)
              a   = MixtPerf_C_GRT(gProducts,gcProducts,T)

              pDvMixt(DV_MIXT_PRES,icg) = p
              pDvMixt(DV_MIXT_TEMP,icg) = T
              pDvMixt(DV_MIXT_SOUN,icg) = a

! ----------- Set Gv for products ----------------------------------------------
              pGvMixt(GV_MIXT_MOL,icg*indMol) = mwProducts
              pGvMixt(GV_MIXT_CP ,icg*indCp ) = cpProducts
            END IF ! flameLoc
          END IF
        END DO ! icg

        IF ( flameLoc > xFinal ) THEN
          global%pbaBurnFlag = .FALSE.
        END IF ! flameLoc

! ==============================================================================
!     Default
! ==============================================================================  
  
      CASE DEFAULT     
        CALL ErrorStop(global,ERR_REACHED_DEFAULT,__LINE__)
    END SELECT ! global%casename        

! ******************************************************************************
!   Convert species back to conservative form
! ******************************************************************************

    CALL RFLU_ScalarConvertCvPrim2cons(pRegion,pRegion%spec%cv, &
                                       pRegion%spec%cvState)

! ******************************************************************************
!   End  
! ******************************************************************************

    CALL DeregisterFunction(global)

  END SUBROUTINE SPEC_RFLU_PBA_ProgramBurn








! ******************************************************************************
!
! Purpose: Set rhs for explosive to zero.
!
! Description: None.
!
! Input:
!   region       Data of current region
!
! Output: None.
!
! Notes: None.
!
! ******************************************************************************

  SUBROUTINE SPEC_RFLU_PBA_ZeroResiduals(region)

    IMPLICIT NONE
        
! ******************************************************************************
!   Declarations and definitions
! ******************************************************************************
    
! ==============================================================================
!   Arguments
! ==============================================================================
   
    TYPE(t_region), TARGET :: region
    
! ==============================================================================
!   Locals
! ==============================================================================

    CHARACTER(CHRLEN) :: errorString
    INTEGER :: icg,iCvSpecExplosive,iVarScal
    REAL(RFREAL) :: nTol,x,y,YExplosive
    REAL(RFREAL), DIMENSION(:,:), POINTER :: pCvMixt,pCvSpec,pRhsMixt,pRhsSpec
    TYPE(t_global), POINTER :: global
    TYPE(t_grid), POINTER :: pGrid
    TYPE(t_mixt_input), POINTER :: pMixtInput
    TYPE(t_spec_input), POINTER :: pSpecInput
    TYPE(t_region), POINTER :: pRegion

! ******************************************************************************
!   Start, if pbaBurnFlag is FALSE then no need to go further
! ******************************************************************************

    pRegion => region%pRegion
    global  => pRegion%global

    IF ( global%pbaBurnFlag .EQV. .FALSE. ) THEN
      RETURN
    END IF ! flameLoc

    CALL RegisterFunction(global,'SPEC_RFLU_PBA_ZeroResiduals',__FILE__)

! ******************************************************************************
!   Set pointers and variables 
! ******************************************************************************

    pGrid      => pRegion%grid
    pCvMixt    => pRegion%mixt%cv
    pCvSpec    => pRegion%spec%cv
    pMixtInput => pRegion%mixtInput
    pSpecInput => pRegion%specInput
    pRhsMixt   => pRegion%mixt%rhs
    pRhsSpec   => pRegion%spec%rhs

    nTol = 1.0E-14_RFREAL

! ******************************************************************************
!   Use program burn algorithm based on case name
! ******************************************************************************

    SELECT CASE ( global%casename )

! ==============================================================================
!     Jenkins 2D planar case
! ==============================================================================

      CASE ( "jenkins2d" )
        iCvSpecExplosive = SPEC_GetSpeciesIndex(global,pSpecInput,'EXPLOSIVE')

        DO icg = 1,pGrid%nCellsTot
          x = pGrid%cofg(XCOORD,icg)
          y = pGrid%cofg(YCOORD,icg)

          IF ( x >= pMixtInput%prepRealVal1 .AND. &
               x <= pMixtInput%prepRealVal2 .AND. &
               y >= pMixtInput%prepRealVal3 .AND. &
               y <= pMixtInput%prepRealVal4 ) THEN             
            YExplosive = pCvSpec(iCvSpecExplosive,icg)/pCvMixt(CV_MIXT_DENS,icg)
            IF ( ABS(1.0_RFREAL - YExplosive) < nTol ) THEN
              pRhsMixt(CV_MIXT_DENS,icg) = 0.0_RFREAL 
              pRhsMixt(CV_MIXT_XMOM,icg) = 0.0_RFREAL
              pRhsMixt(CV_MIXT_YMOM,icg) = 0.0_RFREAL
              pRhsMixt(CV_MIXT_ZMOM,icg) = 0.0_RFREAL
              pRhsMixt(CV_MIXT_ENER,icg) = 0.0_RFREAL

              DO iVarScal = 1,pSpecInput%nSpecies
                pRhsSpec(iVarScal,icg) = 0.0_RFREAL
              END DO ! iVarScal
            END IF ! YExplosive
          END IF ! x,y
        END DO ! icg

! ==============================================================================
!     Default
! ==============================================================================  
  
      CASE DEFAULT     
        CALL ErrorStop(global,ERR_REACHED_DEFAULT,__LINE__)
    END SELECT ! global%casename        

! ******************************************************************************
!   End  
! ******************************************************************************

    CALL DeregisterFunction(global)

  END SUBROUTINE SPEC_RFLU_PBA_ZeroResiduals








END MODULE SPEC_RFLU_ModPBA

! ******************************************************************************
!
! RCS Revision history:
!
! $Log: SPEC_RFLU_ModPBA.F90_SAVE_20110315,v $
! Revision 1.1.1.1  2015/01/23 22:57:50  tbanerjee
! merged rocflu micro and macro
!
! Revision 1.1.1.1  2014/07/15 14:31:38  brollin
! New Stable version
!
!
! ******************************************************************************

